{ config, options, pkgs, lib, ... }:
let
  inherit (lib) util mkIf mapAttrsToList concatStringsSep concatMapStrings;
  inherit (pkgs) zsh zsh-prezto exa;

  cfg = config.modules.shell.zsh;
  configDir = config.dotfiles.configDir;
in {
  options.modules.shell.zsh = with lib.types; {
    enable = util.mkBoolOpt false;

    aliases = util.mkOpt (attrsOf (either str path)) { };

    rcInit = util.mkOpt' lines "" ''
      Zsh lines to be written to $XDG_CONFIG_HOME/zsh/extra.zshrc and sourced by
      $XDG_CONFIG_HOME/zsh/.zshrc
    '';
    envInit = util.mkOpt' lines "" ''
      Zsh lines to be written to $XDG_CONFIG_HOME/zsh/extra.zshenv and sourced
      by $XDG_CONFIG_HOME/zsh/.zshenv
    '';

    rcFiles = util.mkOpt (listOf (either str path)) [ ];
    envFiles = util.mkOpt (listOf (either str path)) [ ];
  };

  config = mkIf cfg.enable {
    programs.zsh = {
      enable = true;
      enableCompletion = true;
    };

    environment.systemPackages = [
      zsh
      zsh-prezto
      exa
    ];

    env = {
      ZDOTDIR = "$XDG_CONFIG_HOME/zsh";
      ZSH_CACHE = "$XDG_CACHE_HOME/zsh";
      ZPREZTO_DIR = "$XDG_DATA_HOME/zprezto";
    };

    home.configFile = {
      # Write it recursively so other modules can write files to it
      "zsh" = {
        # source = "${configDir}/zsh";
        source = util.mkOutOfStoreSymlink "${configDir}/zsh";
        recursive = true;
      };

      # Why am I creating extra.zsh{rc,env} when I could be using extraInit?
      # Because extraInit generates those files in /etc/profile, and mine just
      # write the files to ~/.config/zsh; where it's easier to edit and tweak
      # them in case of issues or when experimenting.
      "zsh/extra.zshrc".text = let
        aliasLines = mapAttrsToList (n: v: ''alias ${n}="${v}"'') cfg.aliases;
      in ''
        # Autogenerated file, do not edit.
        ${concatStringsSep "\n" aliasLines}
        ${concatMapStrings (path: ''
          source '${path}'
        '') cfg.rcFiles}
        ${cfg.rcInit}
      '';

      "zsh/extra.zshenv".text = ''
        # Autogenerated file, do not edit.
        ${concatMapStrings (path: ''
          source '${path}'
        '') cfg.envFiles}
        ${cfg.envInit}
      '';
    };

    home.dataFile = {
      "zprezto" = {
        source = "${pkgs.zsh-prezto}/share/zsh-prezto";
        recursive = true;
      };
    };
  };
}
